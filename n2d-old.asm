
; NRV2D DECOMPRESSOR FOR INTEL 8080
; IN:	BC - PACKED DATA SOURCE ADDRESS
;	DE - UNPACKED DATA DESTINATION ADDRESS
; OUT:	BC - POINTS TO THE POSITION AFTER LAST BYTE OF INPUT DATA
;	DE - POSITION AFTER LAST BYTE OF OUTPUT BUFFER

; IMPORTANT. THIS CODE DOES NOT ACCEPT ORIGINAL PACKED BYTE SEQUENCES'S FINAL
; MARKER. 32-BIT OFFSET USED IN ORIGINAL ALGORITHM IS DIFFICULT TO ENCODE ON
; 8-BIT PROCESSOR, THE COMPRESSION SUBROUTINE `ucl_nrv_99_compress` MUST BE
; MODIFICATED TO STORE SHORT FINAL CODE PREFIX AND WORK PROPERLY:
; UCL VERSION: 1.03, FILE `src\n2_99.ch`, LINE #605, ORIGINAL:
;	code_prefix_ss12(c, UCL_UINT32_C(0x1000000));
; SHOULD BE:
;	code_prefix_ss12(c, UCL_UINT32_C(0x100));

N2DUNPK:
	LXI	H, -1
	PUSH	H
	STC
	JMP	L1C

L17:	MOV	L, A
	LDAX	B
	STAX	D
	INX	B
	INX	D
	MOV	A, L
L18:	ADD	A
	JNZ	L21
L1C:	LDAX	B
	INX	B
	ADC	A
L21:	JC	L17
	PUSH	D

	LXI	H, 1
L26:	ADD	A
	JNZ	L2F
	LDAX	B
	INX	B
	ADC	A
L2F:	JC	DI1
	DAD	H
	JMP	OV1
DI1:	DAD	H
	INR	L
OV1:	ADD	A
	JNZ	L3A
	LDAX	B
	INX	B
	ADC	A
L3A:	JC	L4A
	DCX	H
	ADD	A
	JNZ	L46
	LDAX	B
	INX	B
	ADC	A
L46:	JC	DI2
	DAD	H
	JMP	L26
DI2:	DAD	H
	INR	L
	JMP	L26

L4A:	MOV	E, A
	MOV	A, L
	SUI	3
	MOV	L, A
	MOV	A, H
	SBI	0
	MOV	H, A
	JC	L5E
	MOV	H, L
	LDAX	B
	INX	B
	CMA
	MOV	L, A
	MOV	A, H
	CMA
	MOV	H, A
	ORA	L
	JZ	LA5	; EXIT
	MOV	A, H
	STC
	RAR
	MOV	H, A
	MOV	A, L
	RAR
	MOV	L, A

	MOV	A, E
	XCHG
	POP	H
	INX	SP
	INX	SP
	PUSH	D
	PUSH	H
	JC	SVC
	DAD	D
	ORA	A
	JMP	OVC
SVC:	DAD	D
	STC
OVC:	PUSH	H
	LXI	H, 0
	JMP	L67

L5E:	MOV	A, E
	POP	H
	POP	D
	PUSH	D
	PUSH	H
	DAD	D
	PUSH	H
	LXI	H, 0

	ADD	A
	JNZ	L67
	LDAX	B
	INX	B
	ADC	A
L67:	JC	DI3
	DAD	H
	JMP	OV3
DI3:	DAD	H
	INR	L
OV3:	ADD	A
	JNZ	L72
	LDAX	B
	INX	B
	ADC	A
L72:	JC	DI4
	DAD	H
	DCR	L
	JMP	IL
DI4:	DAD	H
IL:	INR	L
	JNZ	L90
	DCR	H
	INR	H
	JNZ	L90
	INX	H
L77:	ADD	A
	JNZ	L80
	LDAX	B
	INX	B
	ADC	A
L80:	JC	DI5
	DAD	H
	JMP	OV5
DI5:	DAD	H
	INR	L
OV5:	ADD	A
	JNZ	L8B
	LDAX	B
	INX	B
	ADC	A
L8B:	JNC	L77
	INX	H
	INX	H

L90:	MOV	E, A
	MOV	A, D
	CPI	0FBH
	JNC	NOC
	INX	H
NOC:	INX	H

	MOV	A, E
	MOV	E, C
	MOV	D, B
	XCHG
	MOV	C, E
	MOV	B, D
	POP	D
	XTHL
	PUSH	PSW

	INR	B
NXMV:	LDAX	D
	MOV	M, A
	INX	D
	INX	H
	DCR	C
	JNZ	NXMV
	DCR	B
	JNZ	NXMV

	POP	PSW
	POP	B
	XCHG
	JMP	L18

LA5:	POP	D
	POP	PSW
	RET

END
